/*
Copyright Â© 2024 Arnab Phukan <iamarnab.phukan@gmail.com>

Licensed under the Apache License, Version 2.0 (the "License");
you may not use this file except in compliance with the License.
You may obtain a copy of the License at

	http://www.apache.org/licenses/LICENSE-2.0

Unless required by applicable law or agreed to in writing, software
distributed under the License is distributed on an "AS IS" BASIS,
WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
See the License for the specific language governing permissions and
limitations under the License.
*/

/* Lexical elements */

_letter : 'a'-'z' | 'A'-'Z' ;

_digit : '0'-'9' ;

_idchar : _letter | _digit | '_' | '/' ;

id : (_letter | '_') {_idchar} ;

int : _digit {_digit} ;

float : _digit {_digit} '.' {_digit} ;

quotedstring : '"' (.) {.} '"' ;

!whitespace : ' ' | '\t' | '\n' | '\r' ;

!comment : _lineComment | _blockComment ;

_lineComment : ';' {.} '\n' ;

_blockComment : '/' 'm' '/' {. | '/'} '/' '/' ;

/* Syntax elements */

<<
import (
	"github.com/Solarcode-org/Orion/lib/ast"
	"github.com/Solarcode-org/Orion/lib/token"
)
>>

FuncCallList :
    FuncCall                  << ast.NewFuncCallList($0) >>
    | FuncCallList FuncCall   << ast.AppendFuncCall($0, $1) >>
;

FuncCall :
      id "(" DataList ")"       << ast.NewFuncCall($T0, $2) >>
    | "let" id "=" Data         << ast.NewFuncCallOneArg($T1, $3) >>
    | "get" Data                << ast.NewFuncCallOneArg($T0, $1) >>
;

DataList :
      Data                      << ast.NewDataList($0) >>
    | DataList "," Data         << ast.AppendData($0, $2) >>
;

Data :
      quotedstring      << ast.NewData($T0, ast.String) >>
    | id                << ast.NewData($T0, ast.Ident) >>
    | FuncCall          << ast.NewFuncData($0) >>
    | int               << ast.NewData($T0, ast.Int) >>
    | float             << ast.NewData($T0, ast.Float) >>
;
